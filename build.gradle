plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'java-library-distribution'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

group = "dev.atchison"
version = '1.0.4.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation("com.zendesk:mysql-binlog-connector-java:0.23.2")
    implementation("org.mariadb.jdbc:mariadb-java-client:2.7.0")
    implementation("org.slf4j:slf4j-api:1.7.30")

    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.assertj:assertj-core:3.17.2")
    testImplementation("org.mockito:mockito-core:3.3.3")
    testImplementation("org.testcontainers:testcontainers:1.15.1")
    testImplementation("org.testcontainers:mariadb:1.15.1")
    testImplementation "org.testcontainers:junit-jupiter:1.15.1"
}

def test = tasks.named("test") {
    useJUnitPlatform {
        excludeTags "integration"
    }
}

def integrationTest = tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name.set("mariadb-cdc")
                description.set("java library for using the MariaDB binary log")
                url.set("https://github.com/TheKnowles/mariadb-cdc")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/license/mit/")
                    }
                }
                developers {
                    developer {
                        id.set("TheKnowles")
                        name.set("Knowles Atchison, Jr")
                        email.set("katchisonjr@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:git:https://github.com/TheKnowles/mariadb-cdc.git")
                    developerConnection.set("scm:git:https://github.com/TheKnowles/mariadb-cdc.git")
                    url.set("https://github.com/TheKnowles/mariadb-cdc")
                }
            }
        }
    }
}

// These must be set in gradle.properties:
// sonatypeUsername=
// sonatypePassword=
nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

// These must be set in gradle.properties:
// signing.keyId=
// signing.password=
// signing.secretKeyRingFile=
signing {
    sign publishing.publications.mavenJava
}

